plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'kotlinx-serialization'
}

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "com.jasperapps.happyfriend"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    flavorDimensions "version"
    productFlavors {
        full
        demoAbstract
        demoEng
        demoUkr
    }
    sourceSets {
        full {
            java.srcDirs = ['src/full/java']
            res.srcDirs = ['src/full/java/res']
        }
        demoAbstract {
            java.srcDirs = ['src/demoAbstract/java']
            res.srcDirs = ['src/demoAbstract/java/res']
        }
        demoEng {
            java.srcDirs = ['src/demoAbstract/java']
            res.srcDirs = ['src/demoAbstract/java/res']
            assets.srcDirs 'src/demoEng/assets'
        }
        demoUkr {
            java.srcDirs = ['src/demoAbstract/java']
            res.srcDirs = ['src/demoAbstract/java/res']
            assets.srcDirs 'src/demoUkr/assets'
        }
    }
    variantFilter { variant ->
        variant.getFlavors().each { flavor ->
            if (flavor.name == 'demoAbstract') {
                variant.setIgnore(true)
            }
        }
    }
}

dependencies {

    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.7.10'

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.1'

    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation platform('com.google.firebase:firebase-bom:30.3.2')

    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation 'com.squareup.inject:assisted-inject-annotations-dagger2:0.8.1'
    kapt 'com.squareup.inject:assisted-inject-processor-dagger2:0.8.1'

    // Need to use old version due to compile error on Apple M1 chip.
    // See: https://stackoverflow.com/a/62739831/4543133
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.6'

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.0")

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'org.robolectric:robolectric:4.7'
    testImplementation "io.mockk:mockk:1.12.5"

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

apply from: "../ktlint.gradle"